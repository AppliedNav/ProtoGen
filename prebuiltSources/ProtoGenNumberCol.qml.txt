import QtQuick 2.9
import QtQuick.Controls 2.3

Column {
    id: control

    property real val: 0
    property alias hasLabel: controlRow.visible
    property alias label: protoGenNumberLabel.text
    property alias units: protoGenNumberUnits.text
    property string comment: ""

    property real scaler: 1.0

    height: protoGenNumberLabel.height + protoGenNumberValue.height + control.spacing
    spacing: controlRow.visible ? 5 : 0

    Component.onCompleted: {
        if (control.units.startsWith("rad"))
        {
            scaler = 180.0/Math.PI
            control.units = control.units.replace("rad", "deg")
        }
    }

    Item {
        height: controlRow.visible ? 0 : 10
    }

    Row {
        id: controlRow
        spacing: 5
        width: control.width
        height: visible ? protoGenNumberLabel.height : 0
        Label {
            id: protoGenNumberLabel
            clip: true
            font: globalProps.fontRegular
            width: control.width - protoGenNumberUnits.width - controlRow.spacing
            height: controlRow.visible ? 24 : 0
            verticalAlignment: Text.AlignVCenter
            horizontalAlignment: Text.AlignHCenter
            ToolTip {
                text: control.comment
                visible: ("" !== control.comment) ? mouseArea.containsMouse : false
            }
            MouseArea {
                id: mouseArea
                anchors.fill: parent
                hoverEnabled: true
            }
        }
        Label {
            id: protoGenNumberUnits
            clip: true
            font: globalProps.fontRegular
            width: ('' !== protoGenNumberUnits.text) ? 0.3 * control.width : 0
            height: protoGenNumberLabel.height
            verticalAlignment: Text.AlignVCenter
            horizontalAlignment: Text.AlignHCenter
            elide: Text.ElideRight
            MouseArea {
                id: unitsMouseArea
                anchors.fill: parent
                hoverEnabled: true
            }
            ToolTip {
                text: protoGenNumberUnits.text
                visible: protoGenNumberUnits.truncated && unitsMouseArea.containsMouse
            }
        }
    }

    TextField {
        id: protoGenNumberValue
        anchors {
            left: parent.left
            leftMargin: 5
            right: parent.right
            rightMargin: 5
        }
        validator: globalProps.doubleValidator
        font: globalProps.fontRegular
        verticalAlignment: Text.AlignVCenter
        horizontalAlignment: Text.AlignHCenter
        rightPadding: 5
        topPadding: 5
        bottomPadding: 5
        onTextChanged: parent.parent.synchro = false
        background: Rectangle {
            anchors.fill: parent
            color: protoGenNumberValue.focus ? "gray" : "transparent"
            border.color: protoGenNumberValue.focus ? "white" : "gray"
        }
        inputMethodHints: Qt.ImhFormattedNumbersOnly
        selectByMouse: true
        text: (control.val*control.scaler).toFixed(globalProps.precision)
        onAccepted: focus = false
        onEditingFinished: control.val = Number(text)/control.scaler
        onActiveFocusChanged: {if (activeFocus) selectAll()}
    }
}
